/*
  Pizza delivery System - UML class diagram written in Umple
*/

//Namespace for core of the system.
namespace ca.mcgill.ecse.pds.model;

use PDSPersistence.ump;

class PDS {
 1 <@>- * Pizza pizzas;
 1 <@>- * Customer customers;
 1 <@>- * Ingredient ingredients;
 1 <@>- * Order orders;
 1 <@>- 1 Menu menu;
}

//Pizza abstract
class Pizza {
  //abstract;
  Integer size = 12;
  * -> 1..* Ingredient ingredients;
  Float price;
  before constructor, setPrice {
    if (aPrice <= 0.0f ) {
	  throw new RuntimeException("The price for a pizza cannot be less than zero.");
	}
  }
  //abstract float getPrice();
}

class Menu {
 1 <@>- * MenuPizza menupizzas;
}

class MenuPizza {
  isA Pizza;
  String name;
  before constructor, setName {
    if (aName == null || aName.length() == 0) {
	  throw new RuntimeException("The name for a pizza on the Menu cannot be empty.");
	}
  }
  Float calorieCount;
  before constructor, setCalorieCount {
    if (aCalorieCount <= 0.0f ) {
	  throw new RuntimeException("The calorie count for a pizza on the Menu cannot be less than zero.");
	}
  }

  float getPrice(){
  	return super.getPrice();
  }
}

class CustomPizza {
  isA Pizza;
  float getPrice(){
  	return super.getPrice();
  }
}

class Ingredient {
  String name;
  before constructor, setName {
    if (aName == null || aName.length() == 0) {
	  throw new RuntimeException("The name of an ingredient cannot be empty.");
	}
  }
  Float price;
  before constructor, setPrice {
    if (aPrice <= 0.0f) {
	  throw new RuntimeException("The price cannot be less than zero");
	}
  }
}
class Customer {
  autounique id;
  String name;
  String phoneNumber;
  String emailAddress;
  String deliveryAddress;
  1 -- * Order orders;
}

class Order {
  autounique number;
  Date date;
  1 <@>- * OrderItem orderItem;
  Float totalPrice;
  Boolean isDelivered = false; 
}

class OrderItem {
 Float price;
  * -> 1 Pizza pizza;
  1 <@>- * Customization customizations;
}

class Customization {
  enum Type { Add, Remove , Replace }  
  Type type;
  * -> * Ingredient ingredients;
}
